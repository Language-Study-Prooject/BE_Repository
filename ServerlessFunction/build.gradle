plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
}

group = 'com.mzc.secondproject.serverless'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // AWS Lambda Core
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.3'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.4'

    // AWS SDK v2
    implementation platform('software.amazon.awssdk:bom:2.24.0')
    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:dynamodb-enhanced'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:polly'
    implementation 'software.amazon.awssdk:sns'
    implementation 'software.amazon.awssdk:bedrockruntime'
    implementation 'software.amazon.awssdk:comprehend'
    implementation 'software.amazon.awssdk:apigatewaymanagementapi'
    implementation 'software.amazon.awssdk:url-connection-client'
    implementation 'software.amazon.awssdk:ssm'
    implementation 'software.amazon.awssdk:scheduler'

    // AWS X-Ray SDK (다운스트림 서비스 추적용)
    implementation 'com.amazonaws:aws-xray-recorder-sdk-core:2.15.0'
    implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2:2.15.0'

    // JSON Processing
    implementation 'com.google.code.gson:gson:2.10.1'

    // Jakarta Bean Validation
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'org.glassfish.expressly:expressly:5.0.0'

    // Password Hashing
    implementation 'org.mindrot:jbcrypt:0.4'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Logging
    implementation 'com.amazonaws:aws-lambda-java-log4j2:1.6.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.22.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.mockito:mockito-core:5.8.0'

    // Groovy & Spock
    testImplementation 'org.apache.groovy:groovy-all:4.0.15'
    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-4.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip
